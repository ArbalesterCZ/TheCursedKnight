void OnStart()
{
	AddUseItemCallback("rope_on_crank", "rope", "Crank", "UseRopeOnCrank", true);

	AddEntityCollideCallback("Player", "FirstFloorArea", "OnColliderFirstFloor", true, 1);
	AddEntityCollideCallback("Player", "AreaHatchDoorClose", "OnColliderHatchDoorClose", true, 1);
	AddEntityCollideCallback("Player", "AreaLivingStatue", "OnColliderLivingStatue", true, 1);	

	if(!HasItem("Lantern")) GiveItemFromFile("Lantern", "lantern.ent");
	if(!HasItem("Container")) GiveItemFromFile("Container", "chemical_container.ent");
	if(!HasItem("HammerChipper")) GiveItem("HammerChipper", "Puzzle", "stone_hammer_chipper", "stone_hammer_chipper.tga", 0);

	PlayMusic("06_amb.ogg", true, 0.8f, 0, 0, true);
	SetMapDisplayNameEntry("Warehouse");
	AutoSave();
}

void OnLeave()
{
	StopMusic(0.0f, 5);
	SetGlobalVarInt("ForestSubChapterComplete", 1);
	SetupLoadScreen("Loading", "OnLeaveWarehouse", 0, "forest.jpg");
}

void OnPickCrowbar(string &in entityName, string &in type)
{
	PlaySoundAtEntity("", "unlock_door.snt", "level_cistern_1", 0, false);
	SetLevelDoorLocked("level_cistern_1", false);

	StartPlayerLookAt("level_cistern_1", 2.5f, 3.0f, "");	
	PlaySoundAtEntity("DoorBoom", "break_wood_metal.snt", "level_cistern_1", 0, false);

	SetEntityActive("AreaLivingStatue", true);

	AddTimer("react_pant.snt", 0.35f, "TimerScare");
	AddTimer("StopLookAtCloseDoor", 1.25f, "TimerLookComplete");
	AddTimer("scare_steps_big.snt", 2.25f, "TimerScare");
}

void UseRopeOnCrank(string &in asItem, string &in asEntity)
{
	RemoveItem(asItem);
	SetEntityActive("crank_rope", true);
	PlaySoundAtEntity("RopeStrain", "06_rope_strain.snt", asEntity, 0, false);
	SetWheelAngle(asEntity, 0.0f, false);
	for(int i=0; i<3; i++)
		InteractConnectPropWithRope("rope_connection_"+i, asEntity, "RopeArea_"+i, false, 1.0f, 1.0f, 1.0f, false, 0);

	CompleteQuest(asEntity, asEntity);
}

void OnColliderFirstFloor(string &in asParent, string &in asChild, int alState)
{
	CompleteQuest("LargeDoor", "LargeDoor");
	GiveSanityBoostSmall();
}

void OnColliderHatchDoorClose(string &in asParent, string &in asChild, int alState)
{
	SetEntityActive("LargeDoor", false);
	SetEntityActive("large_door_static", true);
	PlaySoundAtEntity("DoorBoom", "break_wood.snt", "LargeDoor", 0, false);
	CreateParticleSystemAtEntity("DirtyImpact", "ps_break_mansionbase_wall.ps", "LargeDoor", false);
	StartPlayerLookAt("LargeDoor", 2.5f, 3.0f, "");

	AddTimer("react_pant.snt", 0.35f, "TimerScare");
	AddTimer("StopLookAtCloseDoor", 1.25f, "TimerLookComplete");
}

void OnColliderLivingStatue(string &in asParent, string &in asChild, int alState)
{
	SwapStatue(true);

	PlaySoundAtEntity("StatueLaugh", "21_intro_scream.snt", "FleshyStatue", 0, false);	
	StartPlayerLookAt("FleshyStatue", 2.5f, 3.0f, "");
	GiveSanityDamage(15.0f, true);

	CreateParticleSystemAtEntity("StatueSwapEffect", "blood.ps", "FleshyStatue", false);
	CreateParticleSystemAtEntity("StatueSwapEffect", "ps_break_mansionbase_wall.ps", "FleshyStatue", false);

	AddTimer("react_scare.snt", 0.2f, "TimerScare");
	AddTimer("StopLookAtLivingStatue", 0.5f, "TimerLookComplete");
	AddTimer("SwapStatue", 1.25f, "TimerSwapStatue");
}

void TimerSwapStatue(string &in soundFile)
{
	SwapStatue(false);
}

void SwapStatue(bool evil)
{
	SetEntityActive("FleshyStatue", evil);
	SetEntityActive("LivingStatue", !evil);
}

void OnInteractDoor(string &in door)
{
	if(!QuestIsCompleted(door))
	{
		SetMessage("Hints", door, 5.0f);
		AddQuest(door, door);
	}
}

void TimerLookComplete(string &in asTimer)
{
	StopPlayerLookAt();
}

void TimerScare(string &in soundFile)
{
	PlayGuiSound(soundFile, 1.0f);
	ChangePlayerStateToNormal();
}
